plugins {
    id 'java-library'
    id "io.freefair.lombok" version "6.3.0"
    id 'org.springframework.boot' version '2.6.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'jacoco'
}

group = 'com.matrix'
sourceCompatibility = JavaVersion.VERSION_11

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://s3-us-west-2.amazonaws.com/dynamodb-local/release'
    }
}

dependencies {
    // inner dependency
    implementation 'com.matrix:chainsync-dynamodb-sdk'
    implementation 'com.matrix:chainsync-common-model'
    implementation 'com.matrix:chainsync-dao'
    implementation 'com.matrix:chainsync-common-model-grpc'
    implementation 'com.matrix:chainsync-blockchain-event-handler-grpc'

    // common dependency
    implementation 'net.devh:grpc-client-spring-boot-starter:2.13.1.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    implementation 'com.github.xiaoymin:knife4j-spring-boot-starter:3.0.3'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.amazonaws:aws-java-sdk-s3:1.12.220'
    implementation 'org.web3j:core:4.9.0'
    implementation 'commons-validator:commons-validator:1.7'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'

    // test only dependency
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    testLogging {
        showStandardStreams = true
    }
    exclude '**/*IntegrationTest*'
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

task integrationTest(type: Test) {
    testLogging {
        showStandardStreams = true
    }
    useJUnitPlatform()
}

jacocoTestReport {
    reports {
        html.enabled true
        html.destination file("${buildDir}/reports/jacoco/html")
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/coverage.xml")
    }
}
